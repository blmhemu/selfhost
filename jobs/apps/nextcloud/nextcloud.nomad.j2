job "nextcloud" {

  region        = "{{ nomad_region }}"
  datacenters   = ["{{ nomad_datacenter }}"]
  type          = "service"

  update {
    healthy_deadline = "15m" # Nextcloud takes some time to initialize
    progress_deadline = "20m"
  }

  group "nextcloud" {

    network {
      mode = "bridge"

      port "http" {
        to = 80
      }

      dns {
        servers = ["9.9.9.9", "149.112.112.112"]
      }
    }

    service {
      name = "nextcloud"
      port = "http"
      tags = [
        "traefik.enable=true",
        "traefik.http.routers.nextcloud.rule=Host(`nextcloud.{{ base_domain }}`)",
        # Redirection
        "traefik.http.middlewares.nextcloud_redirect.redirectregex.regex=/.well-known/(card|cal)dav",
        "traefik.http.middlewares.nextcloud_redirect.redirectregex.replacement=/remote.php/dav/",
        "traefik.http.middlewares.nextcloud_redirect.redirectregex.permanent=true",
        # Headers
        ## CSP
        "traefik.http.middlewares.nextcloud.headers.contentSecurityPolicy=frame-ancestors 'self'",
        "traefik.http.middlewares.nextcloud.headers.customFrameOptionsValue=SAMEORIGIN",
        ## STS stuff
        "traefik.http.middlewares.nextcloud.headers.stsSeconds=155520011",
        "traefik.http.middlewares.nextcloud.headers.stsIncludeSubdomains=true",
        "traefik.http.middlewares.nextcloud.headers.stsPreload=true",
        "traefik.http.routers.nextcloud.middlewares=nextcloud_redirect,nextcloud",
      ]

      check {
        type     = "http"
        path     = "/"
        interval = "60s"
        timeout  = "10s"
      }
    }

    volume "nextcloud-data" {
      type = "csi"
      source = "nextcloud-data"
      access_mode = "multi-node-multi-writer"
      attachment_mode = "file-system"
    }

    task "nextcloud" {
      driver = "docker"

      config {
        image = "nextcloud:24"
        ports = ["http"]
      }

      env {
        POSTGRES_DB               = "{{ nc_db }}"
        POSTGRES_USER             = "{{ nc_db_user }}"
        POSTGRES_PASSWORD         = "{{ nc_db_pass }}"
        POSTGRES_HOST             = "localhost"
        REDIS_HOST                = "localhost"
        NEXTCLOUD_ADMIN_USER      = "{{ nc_admin_user }}"
        NEXTCLOUD_ADMIN_PASSWORD  = "{{ nc_admin_pass }}"
        NEXTCLOUD_TRUSTED_DOMAINS = "nextcloud.{{ base_domain }} ${NOMAD_IP_http}"
        TRUSTED_PROXIES           = "100.64.0.0/10 172.16.0.0/12 192.168.0.0/16 10.0.0.0/8"
      }

      volume_mount {
        volume      = "nextcloud-data"
        destination = "/var/www/html"
        read_only   = false
      }

      resources {
        cpu    = 500
        memory = 512
      }

    }


    volume "nextcloud-db" {
      type      = "csi"
      source    = "nextcloud-pg"
      access_mode = "multi-node-multi-writer"
      attachment_mode = "file-system"
    }

    task "postgres" {
      driver = "docker"

      config {
        image = "postgres:14"
      }

      env {
        POSTGRES_DB       = "{{ nc_db }}"
        POSTGRES_USER     = "{{ nc_db_user }}"
        POSTGRES_PASSWORD = "{{ nc_db_pass }}"
      }

      volume_mount {
        volume      = "nextcloud-db"
        destination = "/var/lib/postgresql/data"
        read_only   = false
      }

      resources {
        cpu    = 250
        memory = 256
      }
    }

    task "redis" {
      driver = "docker"

      config {
        image = "redis:7-alpine"
      }

      resources {
        cpu    = 100
        memory = 128
      }
    }
  }

}
